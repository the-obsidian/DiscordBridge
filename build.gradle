import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }

    configurations.all {
        resolutionStrategy {
            force 'org.ow2.asm:asm:6.0_BETA'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    group = 'gg.obsidian'
    version = '4.0.0'
    description = """Bridge chat between Minecraft and Discord"""
    ext.url = 'https://github.com/the-obsidian/DiscordBridge'
    ext.modid = 'discordbridge-obsidian'
    processResources {
        filter ReplaceTokens, tokens: [
                'DESCRIPTION': project.property('description'),
                'URL'        : project.property('url'),
                'VERSION'    : project.property('version'),
                'MODID'      : project.property('modid')
        ]
    }

    repositories {
        maven { url 'https://github.com/DV8FromTheWorld/Maven-Repository/raw/master/repo' }
        jcenter()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }

    shadowJar {
        classifier = null
        dependencies {
            include(dependency('net.dv8tion:JDA:3.3.1_286'))
            include(dependency(group: 'org.yaml', name: 'snakeyaml', version: '1.19'))
            include(dependency(group: 'org.pegdown', name:'pegdown', version: '1.6.0'))
            include(dependency(group: 'commons-lang', name:'commons-lang', version: '2.3'))
            include(dependency(group: 'com.michaelwflaherty', name: 'cleverbotapi', version: '1.0.1'))
            include(dependency('org.parboiled:parboiled-core'))
            include(dependency('org.parboiled:parboiled-java'))
            include(dependency('com.squareup.okhttp3:okhttp:'))
            include(dependency('com.squareup.okio:okio'))
            include(dependency('com.neovisionaries:nv-websocket-client'))
            include(dependency('org.slf4j:slf4j-api'))
            include(dependency('org.json:json'))
        }
    }

}
