import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext.kotlin_version = '1.1.1'

    repositories {
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'kotlin'

group = 'co.orre'
version = '2.0.1'
description = """Bridge chat between Minecraft and Discord"""
ext.url = 'https://github.com/DiamondIceNS/DiscordBridge'

repositories {
    maven { url 'https://maven-central.storage.googleapis.com' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'http://lutece.paris.fr/nexus/content/repositories/lutece_third_party' }
    maven { url 'https://github.com/DV8FromTheWorld/Maven-Repository/raw/master/repo' }

    jcenter()
}

dependencies {
    compile group: 'org.spigotmc', name: 'spigot-api', version: '1.11.2-R0.1-SNAPSHOT'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.0.0-rc-1036'
    compile group: 'net.dv8tion', name: 'JDA', version: '3.0.BETA2_108'
    compile group: 'com.michaelwflaherty', name: 'cleverbotapi', version: '1.0.1'
    compile group: 'org.pegdown', name:'pegdown', version: '1.6.0'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

processResources {
    filter ReplaceTokens, tokens: [
            'DESCRIPTION': project.property('description'),
            'URL'        : project.property('url'),
            'VERSION'    : project.property('version')
    ]
}

//noinspection GroovyAssignabilityCheck
build.finalizedBy(shadowJar)

shadowJar {
    relocate 'org.apache', 'shadow.apache'
    classifier 'dist'
    dependencies {
        //noinspection GroovyAssignabilityCheck
        exclude(dependency('org.spigotmc:.*:.*'))
    }
    exclude '.cache'
}

task copyFinalJar(type: Copy) {
    from "build/libs/${shadowJar.archiveName}"
    into "build"
    rename(
            shadowJar.archiveName,
            "${project.property('name')}-${project.property('version')}.jar"
    )
}

shadowJar.finalizedBy(copyFinalJar)
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
