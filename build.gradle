import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

allprojects {
    group = 'gg.obsidian'
    version = '3.1.0'
    description = """Bridge chat between Minecraft and Discord"""
    ext.url = 'https://github.com/the-obsidian/DiscordBridge'
}

apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    compile project('discordbridge-bukkit')
    compile project('discordbridge-core')
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.github.johnrengelman.shadow'

    processResources {
        filter ReplaceTokens, tokens: [
                'DESCRIPTION': project.property('description'),
                'URL'        : project.property('url'),
                'VERSION'    : project.property('version')
        ]
    }

    shadowJar {
        dependencies {
            include(dependency("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"))
            include(dependency('net.dv8tion:JDA:3.3.1_286'))
            include(dependency(group: 'org.yaml', name: 'snakeyaml', version: '1.19'))
            include(dependency(group: 'org.pegdown', name:'pegdown', version: '1.6.0'))
            include(dependency(group: 'commons-lang', name:'commons-lang', version: '2.3'))
            include(dependency(group: 'com.michaelwflaherty', name: 'cleverbotapi', version: '1.0.1'))
            include(dependency('org.parboiled:parboiled-core'))
            include(dependency('org.parboiled:parboiled-java'))
            include(dependency('org.ow2.asm:asm'))
            include(dependency('org.ow2.asm:asm-analysis'))
            include(dependency('org.ow2.asm:asm-tree'))
            include(dependency('org.ow2.asm:asm-util'))
            include(dependency('com.squareup.okhttp3:okhttp:'))
            include(dependency('com.squareup.okio:okio'))
            include(dependency('com.neovisionaries:nv-websocket-client'))
            include(dependency('org.slf4j:slf4j-api'))
            include(dependency('org.json:json'))
        }
    }

}